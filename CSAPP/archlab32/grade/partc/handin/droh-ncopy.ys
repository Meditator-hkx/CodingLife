# #################################################################
# ncopy.ys - Copy a src block of len ints to dst.
# Return the number of positive ints (>0) contained in src.
# 
# Include your name and ID here.
# 
# This code unrolls the the baseline optimized inner loop (iaddl+cmov+lu) 
# by a factor of 2.
# #################################################################

# Do not modify this portion
# Function prologue
ncopy:
	pushl %ebp              # Save old frame pointer
	rrmovl %esp,%ebp        # Set up new frame pointer
	pushl %esi              # Save callee-save regs
	pushl %ebx
	pushl %edi
	mrmovl 8(%ebp),%ebx     # src
	mrmovl 12(%ebp),%ecx    # dst
	mrmovl 16(%ebp),%edx    # len

# #################################################################
# You can modify this portion

	xorl %eax,%eax          # count = 0
	iaddl $-2, %edx         # lmu = len - 2
	jl SkipLoop             # If < 0, skip loop

# Main loop.  Copy 2 elements per iteration
Loop:
	mrmovl 0(%ebx), %esi    # read val from src+0
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 0(%ecx)    # Store val at dst+0
	mrmovl 4(%ebx), %esi    # read val from src+1
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 4(%ecx)    # Store val at dst+1
	iaddl $8, %ebx          # src+=2
	iaddl $8, %ecx          # dst+=2
	iaddl $-2, %edx         # lmu-=2
	jge Loop                # if lmu >= 0, goto Loop
SkipLoop:
# Set %edx to number or remaining elements
# (between 0 and 1)
	iaddl $2, %edx          # len = lmu + 2
	# Single step remaining elements
	je Done
SingleLoop:
	mrmovl 0(%ebx), %esi    # read val from src+0
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 0(%ecx)    # Store val at dst+0
	iaddl $4, %ebx          # src++
	iaddl $4, %ecx          # dst++
	iaddl $-1, %edx         # len--
	jg SingleLoop           # if len > 0, goto SingleLoop

# #################################################################

# Do not modify the following section of code
# Function epilogue
Done:
	popl %edi
	popl %ebx
	popl %esi
	rrmovl %ebp, %esp
	popl %ebp
	ret
# ##################################################################
# # Keep the following label at the end of your function
End:
