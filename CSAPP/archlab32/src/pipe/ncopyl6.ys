# #################################################################
# ncopy.ys - Copy a src block of len ints to dst.
# Return the number of positive ints (>0) contained in src.
# 
# Include your name and ID here.
# 
# Describe how and why you modified the baseline code.
# This code unrolls the main loop by a factor of 6.
# #################################################################

# Do not modify this portion
# Function prologue
ncopy:
	pushl %ebp              # Save old frame pointer
	rrmovl %esp,%ebp        # Set up new frame pointer
	pushl %esi              # Save callee-save regs
	pushl %ebx
	pushl %edi
	mrmovl 8(%ebp),%ebx     # src
	mrmovl 12(%ebp),%ecx    # dst
	mrmovl 16(%ebp),%edx    # len

# #################################################################
# You can modify this portion

	xorl %eax,%eax          # count = 0
	iaddl $-6, %edx         # lmu = len - 6
	jl SkipLoop             # If < 0, skip loop

# Main loop.  Copy 6 elements per iteration
Loop:
	mrmovl 0(%ebx), %esi    # read val from src+0
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 0(%ecx)    # Store val at dst+0
	mrmovl 4(%ebx), %esi    # read val from src+1
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 4(%ecx)    # Store val at dst+1
	mrmovl 8(%ebx), %esi    # read val from src+2
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 8(%ecx)    # Store val at dst+2
	mrmovl 12(%ebx), %esi   # read val from src+3
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 12(%ecx)   # Store val at dst+3
	mrmovl 16(%ebx), %esi   # read val from src+4
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 16(%ecx)   # Store val at dst+4
	mrmovl 20(%ebx), %esi   # read val from src+5
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 20(%ecx)   # Store val at dst+5
	iaddl $24, %ebx         # src+=6
	iaddl $24, %ecx         # dst+=6
	iaddl $-6, %edx         # lmu-=6
	jge Loop                # if lmu >= 0, goto Loop
SkipLoop:
# Set %edx to number or remaining elements
# (between 0 and 5)
	iaddl $6, %edx          # len = lmu + 6
	# Single step remaining elements
	je Done
SingleLoop:
	mrmovl 0(%ebx), %esi    # read val from src+0
	rrmovl %eax, %edi       # Copy count
	iaddl $1, %edi          # count+1
	andl %esi, %esi         # Test val
	cmovg %edi, %eax        # If val > 0, count = count+1
	rmmovl %esi, 0(%ecx)    # Store val at dst+0
	iaddl $4, %ebx          # src++
	iaddl $4, %ecx          # dst++
	iaddl $-1, %edx         # len--
	jg SingleLoop           # if len > 0, goto SingleLoop

# #################################################################

# Do not modify the following section of code
# Function epilogue
Done:
	popl %edi
	popl %ebx
	popl %esi
	rrmovl %ebp, %esp
	popl %ebp
	ret
# ##################################################################
# # Keep the following label at the end of your function
End:
